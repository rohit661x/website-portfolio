export const vertexShader = /* glsl */ `
attribute vec2 aPosition;
void main() {
  gl_Position = vec4(aPosition, 0.0, 1.0);
}
`;

// 3D simplex noise: Ian McEwan / Ashima Arts, MIT licence
// https://github.com/ashima/webgl-noise
const simplex = /* glsl */ `
vec3 mod289(vec3 x) { return x - floor(x * (1.0 / 289.0)) * 289.0; }
vec4 mod289(vec4 x) { return x - floor(x * (1.0 / 289.0)) * 289.0; }
vec4 permute(vec4 x) { return mod289(((x * 34.0) + 1.0) * x); }
vec4 taylorInvSqrt(vec4 r) { return 1.79284291400159 - 0.85373472095314 * r; }

float snoise(vec3 v) {
  const vec2 C = vec2(1.0 / 6.0, 1.0 / 3.0);
  const vec4 D = vec4(0.0, 0.5, 1.0, 2.0);
  vec3 i = floor(v + dot(v, C.yyy));
  vec3 x0 = v - i + dot(i, C.xxx);
  vec3 g = step(x0.yzx, x0.xyz);
  vec3 l = 1.0 - g;
  vec3 i1 = min(g.xyz, l.zxy);
  vec3 i2 = max(g.xyz, l.zxy);
  vec3 x1 = x0 - i1 + C.xxx;
  vec3 x2 = x0 - i2 + C.yyy;
  vec3 x3 = x0 - D.yyy;
  i = mod289(i);
  vec4 p = permute(permute(permute(
            i.z + vec4(0.0, i1.z, i2.z, 1.0))
          + i.y + vec4(0.0, i1.y, i2.y, 1.0))
          + i.x + vec4(0.0, i1.x, i2.x, 1.0));
  float n_ = 0.142857142857;
  vec3 ns = n_ * D.wyz - D.xzx;
  vec4 j = p - 49.0 * floor(p * ns.z * ns.z);
  vec4 x_ = floor(j * ns.z);
  vec4 y_ = floor(j - 7.0 * x_);
  vec4 x = x_ * ns.x + ns.yyyy;
  vec4 y = y_ * ns.x + ns.yyyy;
  vec4 h = 1.0 - abs(x) - abs(y);
  vec4 b0 = vec4(x.xy, y.xy);
  vec4 b1 = vec4(x.zw, y.zw);
  vec4 s0 = floor(b0) * 2.0 + 1.0;
  vec4 s1 = floor(b1) * 2.0 + 1.0;
  vec4 sh = -step(h, vec4(0.0));
  vec4 a0 = b0.xzyw + s0.xzyw * sh.xxyy;
  vec4 a1 = b1.xzyw + s1.xzyw * sh.zzww;
  vec3 p0 = vec3(a0.xy, h.x);
  vec3 p1 = vec3(a0.zw, h.y);
  vec3 p2 = vec3(a1.xy, h.z);
  vec3 p3 = vec3(a1.zw, h.w);
  vec4 norm = taylorInvSqrt(vec4(dot(p0, p0), dot(p1, p1), dot(p2, p2), dot(p3, p3)));
  p0 *= norm.x; p1 *= norm.y; p2 *= norm.z; p3 *= norm.w;
  vec4 m = max(0.6 - vec4(dot(x0, x0), dot(x1, x1), dot(x2, x2), dot(x3, x3)), 0.0);
  m = m * m;
  return 42.0 * dot(m * m, vec4(dot(p0, x0), dot(p1, x1), dot(p2, x2), dot(p3, x3)));
}
`;

export const fragmentShader = /* glsl */ `
precision highp float;

uniform vec2 uResolution;
uniform float uTime;
uniform vec2 uCenter;     // glow centre in uv space
uniform float uRadius;    // glow radius (height units)
uniform vec3 uTint;
uniform vec3 uBackground;

${simplex}

// Dave Hoskins' hash without sine: no visible striping on integer coords
float hash(vec2 p) {
  vec3 p3 = fract(vec3(p.xyx) * 0.1031);
  p3 += dot(p3, p3.yzx + 33.33);
  return fract((p3.x + p3.y) * p3.z);
}

float fbm(vec3 p) {
  float sum = 0.0;
  float amp = 0.5;
  for (int i = 0; i < 3; i++) {
    sum += amp * snoise(p);
    p *= 2.03;
    amp *= 0.5;
  }
  return sum;
}

void main() {
  vec2 uv = gl_FragCoord.xy / uResolution;
  float aspect = uResolution.x / uResolution.y;
  vec2 p = (uv - uCenter) * vec2(aspect, 1.0);

  // Soft glow: an ellipse leaning up and to the right
  float c = cos(0.55), s = sin(0.55);
  vec2 r = mat2(c, -s, s, c) * p;
  float d = length(r * vec2(0.8, 1.25));
  float glow = 1.0 - smoothstep(uRadius * 0.15, uRadius, d);

  // Drifting bands: noise stretched across the lean, slowly advected
  float t = uTime;
  vec3 q = vec3(r.x * 1.4, r.y * 3.6, 0.0) + vec3(t * 0.04375, -t * 0.025, t * 0.05625);
  float n = fbm(q) * 0.5 + 0.5;
  float bands = pow(clamp(n, 0.0, 1.0), 2.6);

  float light = glow * (0.1 + 1.1 * bands);

  // Film grain, re-seeded slowly with a crossfade so it shimmers rather than flickers
  float phase = t * 0.4;
  float k = floor(phase);
  vec2 fc = floor(gl_FragCoord.xy);
  float g = mix(hash(fc + vec2(k * 97.13, k * 53.71)), hash(fc + vec2((k + 1.0) * 97.13, (k + 1.0) * 53.71)), smoothstep(0.0, 1.0, fract(phase)));

  float lum = light * g * 1.15;
  // Soft-knee highlight compression
  float knee = 0.5;
  if (lum > knee) lum = knee + (1.0 - knee) * (1.0 - exp(-(lum - knee) / (1.0 - knee)));

  vec3 col = uBackground + mix(vec3(lum), lum * uTint, 0.35);
  gl_FragColor = vec4(col, 1.0);
}
`;
